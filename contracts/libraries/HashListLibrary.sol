pragma solidity 0.5.6;

import 'openzeppelin-eth/contracts/math/SafeMath.sol';

/**
 * @title Hash List library
 * @author Ocean Protocol Team
 * @dev Implementation of the basic functionality of list of hash values.
 * This library allows other contracts to build and maintain lists
 * and also preserves the privacy of the data by accepting only hashed 
 * content (bytes32 based data type)
 */

library HashListLibrary {
    
    using SafeMath for uint256;
    
    struct List {
        address _owner;
        bytes32[] values;
        mapping (bytes32 => uint256) indices;
    }
    
    modifier onlyListOwner(List storage _self)
    {
        require(
            _self._owner == msg.sender || _self._owner == address(0),
            'Invalid whitelist owner'
        );
        _;
    }
    
    /**
     * @dev add index an element then add it to a list
     * @param _self is a pointer to list in the storage
     * @param value is a bytes32 value
     * @return true if value is added successfully
     */
    function add(
        List storage _self,
        bytes32 value
    )
        public
        onlyListOwner(_self)
        returns(bool)
    {
    
        require(
            _self.indices[value] == 0,
            'Value already exists'
        );
        
        if(_self._owner == address(0))
            _self._owner = msg.sender;
        
        _self.values.push(value);
        _self.indices[value] = _self.values.length;
        return true;
    }
    
    /**
     * @dev put an array of elements without indexing
     *      this meant to save gas in case of large arrays
     * @param _self is a pointer to list in the storage
     * @param values is an array of elements value
     * @return true if values are added successfully
     */
    function add(
        List storage _self,
        bytes32[] memory values
    )
        public
        onlyListOwner(_self)
        returns(bool)
    {
        if(_self._owner == address(0))
            _self._owner = msg.sender;
        _self.values = values;
        return true;
    }
    
    /**
     * @dev update the value with a new value and maintain indices
     * @param _self is a pointer to list in the storage
     * @param oldValue is an element value in a list
     * @param newValue new value
     * @return true if value is updated successfully
     */
    function update(
        List storage _self,
        bytes32 oldValue,
        bytes32 newValue
    )
        public
        onlyListOwner(_self)
        returns(bool)
    {
        require(
            _self.indices[oldValue] != 0,
            'Value does not exist'
        );
        
        require(
            oldValue != newValue,
            'Value already exists'
        );
    
        uint256 oldValueIndex = _self.indices[oldValue];
        _self.values[oldValueIndex - 1] = newValue;
        _self.indices[newValue] = oldValueIndex;
        delete _self.indices[oldValue];
        return true;
    }
    
    /**
     * @dev remove value from a list, updates indices, and list size 
     * @param _self is a pointer to list in the storage
     * @param value is an element value in a list
     * @return true if value is removed successfully
     */ 
    function remove(
        List storage _self,
        bytes32 value
    )
        public
        onlyListOwner(_self)
        returns(bool)
    {
        require(
            _self.indices[value] > 0,
            'Failed to remove element from list'
        ); 
        uint256 valueIndex = _self.indices[value].sub(1);
        // copy the last element to this index
        _self.values[valueIndex] = _self.values[_self.values.length.sub(1)];
        // update the index of the last element to the new index
        bytes32 lastElementValue = _self.values[_self.values.length.sub(1)];
        _self.indices[lastElementValue] = _self.indices[value];
        // delete the last element
        delete _self.values[_self.values.length.sub(1)];
        // delete old value from indices
        delete _self.indices[value];
        // update the list size
        _self.values.length--;
        return true;
    }
    
    /**
     * @dev has value by index 
     * @param _self is a pointer to list in the storage
     * @param index is where is value is stored in the list
     * @return the value if exists
     */
    function get(
        List storage _self,
        uint256 index
    )
        public
        view
        returns(bytes32)
    {
        require(
            index > 0 &&
            index <= _self.values.length,
            'Index is out of range'
        );
        return _self.values[index - 1];
    }
    
    /**
     * @dev index is used to map each element value to its index on the list 
     * @param _self is a pointer to list in the storage
     * @param start index is where to start indexing in the list
     * @param end index is where to stop indexing
     * @return true if the sub list is indexed
     */
    function index(
        List storage _self,
        uint256 start,
        uint256 end
    )
        public
        onlyListOwner(_self)
        returns(bool)
    {
        require(
            start > 0,
            'Start index should be greater than zero'
        );
        
        require(
            start <= _self.values.length &&
            end <= _self.values.length,
            'Indices are out of range'
        );
        
        require(
            start <= end,
            'Invalid indices'
        );
        
        bytes32 lastIndexValue = _self.values[_self.values.length - 1];
        require(
            _self.indices[lastIndexValue] != _self.values.length,
            'List is already indexed'
        );
        
        bytes32 endIndexValue = _self.values[end - 1];
        require(
            _self.indices[endIndexValue] != end,
            'Values already are indexed, try different indices'
        );
        
        return _index(_self, start, end);
    }
    
    /**
     * @dev indexOf gets the index of a value in a list
     * @param _self is a pointer to list in the storage
     * @param value is element value in list
     * @return true if the list is indexed
     */
    function indexOf(
        List storage _self,
        bytes32 value 
    )
        public
        view
        returns(uint256)
    {
        require(
            _self.indices[value] != 0,
            'Value does not exist'
        );
        return _self.indices[value];
    }
    
    /**
     * @dev isIndexed checks if the list is indexed
     * @param _self is a pointer to list in the storage
     * @return true if the list is indexed
     */
    function isIndexed(
        List storage _self
    )
        public
        view
        returns(bool)
    {
        bytes32 lastIndexValue = _self.values[_self.values.length - 1];
        if(_self.indices[lastIndexValue] == _self.values.length)
            return true;
        return false;
    }
    
    /**
     * @dev all returns all list elements
     * @param _self is a pointer to list in the storage
     * @return true if the value exists
     */
    function all(
        List storage _self
    )
        public
        view
        returns(bytes32[] memory)
    {
        return _self.values;
    }
    
    /**
     * @dev size returns the list size
     * @param _self is a pointer to list in the storage
     * @param value is element value in list
     * @return true if the value exists
     */
    function has(
        List storage _self,
        bytes32 value
    )
        public
        view
        returns(bool)
    {
        if(_self.indices[value] > 0)
            return true;
        return false;
    }
    
    /**
     * @dev size gets the list size
     * @param _self is a pointer to list in the storage
     * @return total length of the list
     */
    function size(
        List storage _self
    )
        public
        view
        returns(uint256)
    {
        return _self.values.length;
    }
    
    /**
     * @dev size gets the list size
     * @param _self is a pointer to list in the storage
     * @return list owner
     */
    function owner(
        List storage _self
    )
        public
        view
        returns(address)
    {
        return _self._owner;
    }
    
    /**
     * @dev _index assign index to the list elements
     * @param _self is a pointer to list in the storage
     * @param start is the starting index id
     * @param end is the ending index id
     */
    function _index(
        List storage _self,
        uint256 start,
        uint256 end
    )
        private
        returns(bool)
    {
        for(uint256 i = start - 1; i < end; i++)
            _self.indices[_self.values[i]] = i + 1;
        return true;
    }
}
